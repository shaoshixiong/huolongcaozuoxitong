/* 实时时钟.乙 */
/* 作者 : 邵世雄 */
#引入 "主程序.头"

整型 rtc_获取(整型 类型)
{
    静态 无符号 字符 adr[7] = { 0x32, 0x08, 0x07, 0x04, 0x02, 0x00, 0x09 };
    静态 无符号 字符 max[7] = { 0x99, 0x12, 0x31, 0x23, 0x59, 0x60, 0x99 };
	无符号 字符 s, t;
	整型 错, r = 0;
	/*	Type are...
		0 : Year	1 : Month	2 : Day
		3 : Hour	4 : Minute	5 : Second */
	若 (6 <= 类型) 
	{
		/* 6或更多是错误 */
		返回 -1;
	}
	做  
	{
		错 = 0;
		出入流_out8(0x70, adr[类型]);
		s = 出入流_in8(0x71);
		出入流_out8(0x70, adr[类型]);
		t = 出入流_in8(0x71);
		若 (s != t || 9 < (t & 0x0f) || max[类型] < t) 
		{
			/* 显示Hey Hey两次阅读有所不同！ */
			/* 一秒钟之间没有相关性 */
			/* Koitsu也打破了极限？ */
			错++;
		}
		若 (类型 == 0) 
		{
			/* 仅限年份 */
			r = ((t >> 4) * 10 + (t & 0x0f)) * 100;
			类型 = 6;
			错++;		// 差但错误处理
		}
	} 当(错 != 0);
	r += ((t >> 4) * 10 + (t & 0x0f));
	返回 r;
}
